#include "common.fxh"
#include "oit.fxh"

cbuffer cbConstants
{
    Constants g_Constants;
}

Texture2D<uint4> g_OITLayers;

struct PSOutput
{
    float4 Color : SV_TARGET;
};

void main(in PSInput   PSIn,
          out PSOutput PSOut)
{
    float4 Color;
    Color.rgb = ComputeLighting(PSIn.Color.rgb, PSIn.Normal, g_Constants.LightDir.xyz);
    Color.a   = lerp(g_Constants.MinOpacity, g_Constants.MaxOpacity, PSIn.Color.a);
    
    OITLayers Layers;
    Layers.Data = g_OITLayers[uint2(PSIn.Pos.xy)];
    
    float D = PSIn.Pos.z;
    float TailFactor;
    float T = GetOITTransmittance(Layers, D, TailFactor);
    Color.rgb *= T * Color.a * TailFactor;
    
#if CONVERT_PS_OUTPUT_TO_GAMMA
    // Use fast approximation for gamma correction.
    Color.rgb = pow(Color.rgb, float3(1.0 / 2.2, 1.0 / 2.2, 1.0 / 2.2));
#endif
    PSOut.Color = Color;
}
