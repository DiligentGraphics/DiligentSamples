
#include "structures.fxh"

StructuredBuffer<BoxAttribs>  g_BoxAttribs;

[shader("intersection")]
void main()
{
    // In the intersection shader we don't have any information about the intersection.
    // We use the same AABB which was used in BLAS build to calculate the intersection.

    // Get built-in variables.
    float3  instanceOffset = WorldToObject4x3()[3];
    float3  rayDir         = WorldRayDirection();

    // Calculate sphere center and radius.
    BoxAttribs  box       = g_BoxAttribs[InstanceID()];
    float3      boxMin    = float3(box.minX, box.minY, box.minZ);
    float3      boxMax    = float3(box.maxX, box.maxY, box.maxZ);
    float3      boxSize   = boxMax - boxMin;
    float3      center    = (boxMax + boxMin) * 0.5;
    float       radius    = min(boxSize.x, min(boxSize.y, boxSize.z)) * 0.5;

    // Test sphere and ray intersection.
    float3 oc = WorldRayOrigin() - center + instanceOffset;
    float  a  = dot(rayDir, rayDir);
    float  b  = 2.0 * dot(oc, rayDir);
    float  c  = dot(oc, oc) - radius * radius;
    float  d  = b * b - 4 * a * c;

    if (d >= 0)
    {
        // Calculate intersection point and normal.
        float  hitT = (-b - sqrt(d)) / (2.0 * a);
        float3 pos  = WorldRayOrigin() + rayDir * hitT + instanceOffset;

        ProceduralGeomIntersectionAttribs attr;
        attr.Normal = normalize(pos - center);

        // Send hit attributes to the closest hit shader.
        ReportHit(hitT, RAY_KIND_PROCEDURAL_FRONT_FACE, attr);
    }
}
